buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '6.0.+'
}

apply plugin: 'org.spongepowered.mixin'

group = 'com.bobvarioa'
version = '1.0'

java {
    archivesBaseName = 'KubeJSREIRuntime'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: "$minecraft_version"

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'KubeJSREIRuntime'

            // For KubeJS
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                kubejsreiruntime {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'KubeJSREIRuntime'

            // For KubeJS
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                kubejsreiruntime {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'kubejsreiruntime', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                kubejsreiruntime {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }

    maven {
        // Shedaniel's maven (Architectury API, REI and Cloth Config)
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
        }
    }

    maven {
        url = "https://maven.fabricmc.net/"
    }

    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"

    implementation fg.deobf("dev.architectury:architectury-forge:$architectury_version")
    // REI
    implementation fg.deobf("me.shedaniel:RoughlyEnoughItems-forge:$rei_version")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:$cloth_config_version")

    // KubeJS
    implementation fg.deobf("dev.latvian.mods:kubejs-forge:$kubejs_version")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:$rhino_version")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "kubejsreiruntime",
                "Specification-Vendor"    : "BobVarioa",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "BobVarioa",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
